#Maintainer: saber

pkgname=corebox-git
pkgver=2.2
pkgrel=1
pkgdesc="Combination of some common desktop apps"
arch=('x86_64')
url="https://github.com/rahmanshaber/corebox"
license=('GPL2')
depends=( 'qt5-multimedia' 'qt5-charts' 'qt5-x11extras' 'qt5-webengine' )
makedepends=('git' )
provides=('corebox')
conflicts=('corebox')
source=("${pkgname}::git+https://github.com/rahmanshaber/corebox.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  qmake-qt5 ../$pkgname PREFIX=/usr
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install
}
